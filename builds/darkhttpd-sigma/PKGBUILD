# Maintainer: ledti <antegist @ gmail.com>

pkgname=darkhttpd-sigma
_pkgname=darkhttpd
pkgver=1.9
pkgrel=1
pkgdesc="A small and secure static webserver"
arch=('i686' 'x86_64')
url="http://dmr.ath.cx/net/darkhttpd/"
license=('BSD')
depends=('glibc')
backup=('etc/darkhttpd.conf')
source=("$url/$_pkgname-$pkgver.tar.bz2"
        'darkhttpd.conf'
        'darkhttpd.service'
        'darkhttpd.socket')
sha256sums=('33ef622069dc167bcf85e46482388f2d6555e25796f64a6c5b215d939f2b41c8'
            'b8a92f7d73c186cd0d8564c0ebed0cf6d3b438ca870c5511e3b12e2ae0f6417c'
            '5defbee57c0313bb5d8a9bde88a4651d3ca8486bd9005e032820c0b0e52ed4d4'
            '3a5ad507b72c0c37e3255a70a5dcd8c8c79b728efd07a2dc2f6786f9b9cc3c1b')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  # install darkhttpd:
  install -Dm755 "$srcdir/$_pkgname-$pkgver/darkhttpd" "$pkgdir/usr/bin/darkhttpd"

  # install systemd files:
  install -Dm644 "$srcdir/darkhttpd.conf" "$pkgdir/etc/darkhttpd.conf"
  install -Dm644 "$srcdir/darkhttpd.service" "$pkgdir/etc/systemd/system/darkhttpd.service"
  install -Dm644 "$srcdir/darkhttpd.socket" "$pkgdir/etc/systemd/system/darkhttpd.socket"

  # install license"
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 18 darkhttpd.c > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
